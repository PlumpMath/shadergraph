#+SETUPFILE: setup.org

* Contents                                                         :toc_4_gh:
 - [[#glsl-dependency-resolution][GLSL dependency resolution]]
 - [[#glsl-minifier][GLSL minifier]]

* GLSL dependency resolution

All shader functions in this library are defined via simple maps
stored in vars, each grouping source code and dependencies to other
GLSL functions/snippets. The example spec below defines a new function
with two dependencies.

*Note:*

1. These namespaces also need to be =required=. 
2. It's recommended, though optional, to use the =minified= macro for
   all GLSL source snippets to reduce final JS file size (see next
   section).
3. This library is written using Emacs ORG-mode and all GLSL snippets
   here are defined using their own code blocks, which makes development easier.

#+BEGIN_SRC clojure
  (ns example
    (:require
     [thi.ng.glsl.core :include-macros true :refer-macros [minified]]
     [other.namespace :as other]
     [another.namespace :as another]))

  (def spec
    {:deps [other/spec another/spec]
     :src  (minified
            "vec3 myFunc(vec3 a, vec3 b) {
               return ...;
             }")})
#+END_SRC

In order to correctly concatenate the various shader source snippets,
a dependency graph is computed during assembly using Stuart Sierra's
[[https://github.com/stuartsierra/dependency][dependency library]]. The =assemble= function below receives a spec and
returns an expanded GLSL source string with all dependencies in the
correct order.

#+BEGIN_SRC clojure :noweb-ref deps
  (defn build-graph
    ([spec] (build-graph (dep/graph) spec))
    ([g curr]
       (reduce
        (fn [g d] (build-graph (dep/depend g curr d) d))
        g (:deps curr))))

  (defn assemble
    [spec]
    (->> spec
         (build-graph)
         (dep/topo-sort)
         (map :src)
         (apply str)))
#+END_SRC

#+BEGIN_SRC clojure :noweb yes :tangle ../babel/src/thi/ng/glsl/core.cljs :mkdirp yes :padline no
  (ns thi.ng.glsl.core
    (:require
     [clojure.string :as str]
     [com.stuartsierra.dependency :as dep]))

  <<minify>>

  <<deps>>
#+END_SRC

* GLSL minifier

#+BEGIN_SRC clojure :noweb-ref minify
  (defn- minify-line
    [src]
    (let [src (-> src
                  (str/replace #"\s{2,}|\t" "")
                  (str/replace #"\s*(\{|\}|\=|\*|\,|\+|/|\>|\<|\&|\||\[|\]|\(|\)|\-|\!|\;)\s*" "$1"))]
      (if (= \# (first src))
        (str src "\n")
        src)))

  (defn minify
    [src]
    (let [src (-> src
                  (str/replace #"//.*" "")
                  (str/replace #"/\*[\s\S]*?\*/" "")
                  (str/replace #"^\n+" ""))]
      (->> (str/split src #"\n")
           (map minify-line)
           (apply str))))
#+END_SRC

#+BEGIN_SRC clojure :noweb yes :tangle ../babel/src/thi/ng/glsl/core.clj :mkdirp yes :padline no
  (ns thi.ng.glsl.core
    (:require
     [clojure.string :as str]))

  <<minify>>

  (defmacro minified
    [src] `~(minify src))
#+END_SRC

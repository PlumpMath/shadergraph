#+SETUPFILE: setup.org

* Contents                                                         :toc_4_gh:
 - [[#noise--randomness][Noise & randomness]]
     - [[#pseudo-rngs][Pseudo RNGs]]
     - [[#fast-hash32][Fast Hash32]]
     - [[#fractal-noise][Fractal noise]]
     - [[#simplex-noise][Simplex noise]]
     - [[#complete-namespace-definition][Complete namespace definition]]

* Noise & randomness
** Pseudo RNGs

- http://byteblacksmith.com/improvements-to-the-canonical-one-liner-glsl-rand-for-opengl-es-2-0/

#+BEGIN_SRC glsl :noweb-ref random
  highp float random(vec2 co) {
      highp float a = 12.9898;
      highp float b = 78.233;
      highp float c = 43758.5453;
      highp float dt = dot(co.xy, vec2(a, b));
      highp float sn = mod(dt, PI);
      return fract(sin(sn) * c);
  }
#+END_SRC

#+BEGIN_SRC glsl :noweb-ref random-lowp
  float randomLowP(vec2 co) {
    return fract(sin(dot(co.xy, vec2(12.9898, 78.233))) * 43758.5453);
  }
#+END_SRC

#+BEGIN_SRC glsl :noweb-ref hash-n
  float hashN(float n) { return fract(sin(n) * 758.5453); }
#+END_SRC

#+BEGIN_SRC glsl :noweb-ref noise-3
  float noise3(vec3 x) {
    vec3 p = floor(x);
    vec3 f = fract(x);
    float n = p.x + p.y * 57.0 + p.z * 800.0;
    return mix(mix(mix(hashN(n), hashN(n + 1.0), f.x),
                   mix(hashN(n + 57.0), hashN(n + 58.0), f.x), f.y),
               mix(mix(hashN(n + 800.0), hashN(n + 801.0), f.x),
                   mix(hashN(n + 857.0), hashN(n + 858.0), f.x), f.y), f.z);
  }
#+END_SRC

** Fast Hash32

- https://briansharpe.wordpress.com/2011/11/15/a-fast-and-simple-32bit-floating-point-hash-function/
- https://briansharpe.wordpress.com/2013/06/27/adding-a-4th-dimension/

#+BEGIN_SRC glsl :noweb-ref fast-hash32
  vec4 fast_hash32(vec2 pos) {
    // pos is assumed to be an integer coordinate
    const vec2 OFFSET = vec2(403.839172, 377.242706);
    const float DOMAIN = 71.0;
    const float K = 32745.708984;
    const vec2 SCALE = vec2(2.009842, 1.372549);
    vec4 P = vec4(pos, pos + 1.0);
    P = P - floor(P * (1.0 / DOMAIN)) * DOMAIN;
    P = P * SCALE.xyxy + OFFSET.xyxy;
    P *= P;
    return fract(P.xzxz * P.yyww * (1.0 / K));
  }
#+END_SRC

** Fractal noise

#+BEGIN_SRC glsl :noweb-ref fbm-3
  float fbm3(vec3 p) {
    float f = 0.0;
    f += 0.50000 * noise3(p); p *= 2.02;
    f -= 0.25000 * noise3(p); p *= 2.03;
    f += 0.12500 * noise3(p); p *= 2.01;
    f += 0.06250 * noise3(p); p *= 2.04;
    f -= 0.03125 * noise3(p);
    return f / 0.984375;
  }
#+END_SRC
** Simplex noise

#+BEGIN_SRC glsl :noweb-ref snoise
  // Description : Array and textureless GLSL 2D simplex noise function.
  //      Author : Ian McEwan, Ashima Arts.
  //  Maintainer : ijm
  //     Lastmod : 20110822 (ijm)
  //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
  //               Distributed under the MIT License. See LICENSE file.
  //               https://github.com/ashima/webgl-noise
  vec2 mod289(vec2 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
  }

  vec3 mod289(vec3 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
  }

  vec3 permute(vec3 x) {
    return mod289(((x*34.0)+1.0)*x);
  }

  float snoise(vec2 v) {
    const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                        0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                        -0.577350269189626,  // -1.0 + 2.0 * C.x
                        0.024390243902439); // 1.0 / 41.0
    // First corner
    vec2 i  = floor(v + dot(v, C.yy) );
    vec2 x0 = v -   i + dot(i, C.xx);

    // Other corners
    vec2 i1;
    i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
    // x0 = x0 - 0.0 + 0.0 * C.xx ;
    // x1 = x0 - i1 + 1.0 * C.xx ;
    // x2 = x0 - 1.0 + 2.0 * C.xx ;
    vec4 x12 = x0.xyxy + C.xxzz;
    x12.xy -= i1;

    // Permutations
    i = mod289(i); // Avoid truncation effects in permutation
    vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
                      + i.x + vec3(0.0, i1.x, 1.0 ));

    vec3 m = max(0.5 - vec3(dot(x0, x0), dot(x12.xy, x12.xy), dot(x12.zw, x12.zw)), 0.0);
    m = m * m ;
    m = m * m ;

    // Gradients: 41 points uniformly over a line, mapped onto a diamond.
    // The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

    vec3 x = 2.0 * fract(p * C.www) - 1.0;
    vec3 h = abs(x) - 0.5;
    vec3 ox = floor(x + 0.5);
    vec3 a0 = x - ox;

    // Normalise gradients implicitly by scaling m
    // Approximation of: m *= inversesqrt( a0*a0 + h*h );
    m *= 1.79284291400159 - 0.85373472095314 * ( a0 * a0 + h * h );

    // Compute final noise value at P
    vec3 g;
    g.x  = a0.x  * x0.x  + h.x  * x0.y;
    g.yz = a0.yz * x12.xz + h.yz * x12.yw;
    return 130.0 * dot(m, g);
  }
#+END_SRC

** Complete namespace definition

#+BEGIN_SRC clojure :noweb yes :tangle ../babel/src/thi/ng/glsl/noise.cljc :mkdirp yes :padline no
  (ns thi.ng.glsl.noise
    (:require
     [thi.ng.glsl.core :as glsl :include-macros true]))

  (glsl/defglsl random
    nil "
  <<random>>")

  (glsl/defglsl random-low-p
    nil "
  <<random-lowp>>")

  (glsl/defglsl hash-n
    nil "
  <<hash-n>>")

  (glsl/defglsl noise-3
    [hash-n] "
  <<noise-3>>")

  (glsl/defglsl fbm-3
    [noise-3] "
  <<fbm-3>>")

  (glsl/defglsl snoise
    nil "
  <<snoise>>")
#+END_SRC
